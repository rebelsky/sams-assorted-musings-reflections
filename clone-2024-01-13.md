---
title: Cloning structs in Racket
number: 1271
tags: [Miscellaneous](index-misc), technical, Racket, long, rambly
blurb: There must be a better way!
version: 1.0
released: 2024-01-13
current: 
---
This musing serves as a supplement of sorts to an in-process musing on my work implementing a new image library for CSC-151 [1]. Somewhere in that musing [2], I observed that I needed a way to clone the various structures I was building. I started by trying to write a generic `clone` procedure.

It seemed like it was going to be easy. Here's a built-in Racket procedure that I expected to be helpful.

>     (struct-copy id struct-expr fld-id ...)
>       fld-id = [field-id expr]
>              | [field-id #:parent parent-id expr]

> Creates a new instance of the structure type `id` (which is defined via a structure type defining form such as `struct`) with the same field values as the structure produced by `struct-expr`, except that the value of each supplied `field-id` is instead determined by the corresponding `expr`. If `#:parent` is specified, the `parent-id` must be bound to a parent structure type of `id`.

> <https://docs.racket-lang.org/reference/struct-copy.html>

## Attempt the first

As I said, as I understood the task, I thought it would be easy [2]. Racket has an `object-name` procedure that's supposed to give the structure type.

```
> (struct box (contents) #:transparent #:mutable)
> (box 5)
(box 5)
> (define b (box 5))
> b
(box 5)
> (object-name b)
'box
```

So `clone` can just call `struct-copy` with the result of `object-name`. Right?

```
(define clone
  (lambda (val)
    (struct-copy (object-name val) val)))
```

What happens when I enter this?

```
. struct-copy: not an identifier for structure type in: (object-name val)
```

Nope. That doesn't work.

Let's see what happens if I put the object-name in. First, let's try the symbol.

```
> (struct-copy 'box b)
. struct-copy: not an identifier for structure type in: (quote box)
```

Okay, let's drop the tick mark.

```
> (struct-copy box b)
(box 5)
```

That seems to have worked. Let's make sure.

```
> (define c (struct-copy box b))
> c
(box 5)
> (set-box-contents! b "five")
> b
(box "five")
> c
(box 5)
```

Yup, it worked. So how do I get `box` from `'box`?

```
> (struct-copy (eval 'box) b)
. struct-copy: not an identifier for structure type in: (eval (quote box))
> (struct-copy (eval 'struct:box) b)
. struct-copy: not an identifier for structure type in: (eval (quote struct:box))
```

I'm beginning to worry that no procedure will work there. I literally need to supply an identifier. Let's make sure.

```
> (define box-type (lambda () box))
> (box-type)
#<procedure:box>
> (struct-copy (box-type) b)
. struct-copy: not an identifier for structure type in: (box-type)
```

Well, there goes my goal of writing a single procedure. 

## Attempt the second

What next? One strategy is to define a generic interface and then implement a separate `clone` method for each struct. Let's see.

```
(require racket/generic)

;;; (cloneable? val) -> boolean?
;;;   val : any?
;;; Determine if val is cloneable. You can call `clone` on cloneable values.
(define-generics cloneable
  (clone cloneable)
  #:fallbacks
  [(define clone
     (lambda (val)
       (error 'clone "~a is not cloneable" val)))])
```

No errors, yet. Now, let's try some experiments.

```
> (struct container (contents) #:transparent #:mutable)
> (define b (box 5))
> (define c (clone b))
> (define q (container 5))
> (define r (clone q))
. . clone: contract violation:
  expected: cloneable?
  given: (container 5)
  argument position: 1st
```

It seems that I can call `clone` on the struct that I declared as `cloneable` but not the one I didn't declare that way. Let's make sure the clone worked.

```
> b
(box 5)
> c
(box 5)
> (set-box-contents! c "something else")
> b
(box 5)
> c
(box "something else")
```

Yes, we have successfully cloned the object. Yay! However, I think it's a shallow copy [3]. Let me see.

```
> (define bb (box (box "something")))
> (define cc (clone bb))
> bb
(box (box "something"))
> cc
(box (box "something"))
> (set-box-contents! (box-contents cc) "something else")
> bb
(box (box "something else"))
> cc
(box (box "something else"))
```

Yup, it's a shallow copy. I'm okay with that. In any case, it's rare that I mutate objects.

Of course, this approach means that I'll need a `clone` procedure for each `struct` that I'm defining. Fortunately, they are straightforward. Here's an example from my draft of the new image library.

```
(struct %solid-rectangle %solid-polygon (width height)
  #:methods gen:cloneable
  [(define clone
     (lambda (val)
       (struct-copy %solid-rectangle val)))])
```

Here's another

```
(struct %image
  ([desc #:mutable]
   [stru #:mutable]
   [bits #:mutable]
   [pict #:mutable])
  ...
  #:methods gen:cloneable 
  [(define clone
     (lambda (val)
       (struct-copy %image val)))]
  ...)
```

## Attempt the third

I have working code, but it's inelegant working code. I shouldn't have to write nearly the same code again and again and again. More precisely, I shouldn't write nearly the same code again and again and again. I know better.

My generic procedure didn't work. I believe that's because I'm working with the textual name of the struct, rather than anything else. And if I want to manipulate the text of a program, I need to write a macro. I suppose I have to revisit macros [4] in Racket. (Detour while Sam spends a few minutes looking at the documentation.)

Okay. I'm ready.

I'm going to call the macro `struct-cloneable`.

```
(define-syntax struct-cloneable
  (syntax-rules ()
    [(struct-cloneable name ...)
     (struct name ...
             #:methods gen:cloneable
             [(define clone
               (lambda (val)
                 (struct-copy name val)))])]))
```

Let's see if it works.

```
> (struct-cloneable box (contents) #:transparent #:mutable)
> (define b (box "bee"))
> (define c (clone b))
> b
(box "bee")
> c
(box "bee")
> (box-set-contents! c "see")
. . box-set-contents!: undefined;
 cannot reference an identifier before its definition
> (set-box-contents! c "see")
> c
(box "see")
```

Yes, that seems to work. But it feels somehow icky. What if there's another common procedure I'd like to include in a similar way? It feels like I'll get exponential blowup of what I'd call the procedures. I'd much rather be able to write something like `#:cloneable` or even `(cloneable box)` as part of the struct.

## Attempt the fourth

Can I write a macro that turns `(cloneable NAME)` into the appropriate chunk of code? Here's my first try.

```
(define-syntax-rule (cloneable name)
  #:methods gen:cloneable
  [(define clone
    (lambda (val)
      (struct-copy name val)))])
```

Nope, that doesn't work. I didn't expect it to: There are three values in the returned code, and I'm only permitted one.

```
. define-syntax-rule: too many forms in: gen:cloneable
```

The normal hack of turning three values into one is to put them into a block using either `begin` or `let`. However, I'm pretty sure that I need three separate values in the parameters to `struct`. Let's see.

```
(define-syntax-rule (cloneable name)
  [(let ()
     #:methods gen:cloneable
     (define clone
       (lambda (val)
         (struct-copy name val))))])
```

No errors when I compile that [5]. Let's try.

```
> (cloneable box)
. gen:cloneable: illegal use of syntax
  value at phase 1: #<generic-info> in: gen:cloneable
```

Oooh! gen:cloneable is a syntax value. That complicates things. I'm calling this third attempt a failure. What do I have so far?

Attempt one failed. Attempt two worked but is inelegant. Attempt three worked but is differently inelegant. Attempt four failed. I should keep trying.

## Attempt the fifth

That note about syntax values reminds me that I can convert between data and syntax values. Let's see ... I'll start by attempting to build the correct command. 
```
(define-syntax clon
  (lambda (stx)
    (let* ([data (syntax->datum stx)]
           [val (cadr data)]
           [command (list 'struct-copy (object-name val) val)])
      (displayln command)
      (datum->syntax stx (void)))))
```

Does that work?

```
> (struct box (contents) #:transparent #:mutable)
> (define b (box 3))
> (object-name b)
'box
> (clon b)
(struct-copy #f b)
```

Nope. For some reason, I'm getting `#f` for the object name. I assume it has to do with the environment in which `(object-name val)` is evaluated, and I'm too tired or too confused to figure that out. 

Another failure. Maybe cloning is something that I shouldn't be able to do easily---or at least not generically.

## Attempt the sixth

Well, I have two working attempts. I just don't like either of them. Which is less inelegant? [6] Probably the first, in which I declare an interface and then implement it for each object.

While I can't completely shrink the code, maybe I can write a macro that generates part of the code, in particular, the `clone` method for a particular struct. Let's see.

```
(define-syntax-rule (cloneproc name)
  (define clone
    (lambda (val)
      (struct-copy name val))))
```

That seems straightforward enough. Can I use it?

```
> (struct box (contents) #:mutable #:transparent
    #:methods gen:cloneable [(cloneproc box)])
> (define b (box "failure"))
> (define c (clone b))
. . clone: #(struct:box failure) is not cloneable
```

Nope. I get the default `clone` procedure, rather than the one I've just defined.

I give up. I'm going to stick with my first successful solution, even though it's inelegant. Maybe I'll come up with something better after I've let it percolate around my brain for a bit.

## Lessons

### Some lessons for my students who happen to read this musing

_**Sometimes your knowledgable faculty struggle, too.**_

or

_**Perhaps some of your faculty aren't all that knowledgable.**_

or

_**Learn from your failures.**_

or

_**Don't try to do everything yourself. Ask questions!**_

There's a large Racket community out there. Maybe it would be better to ask them. I happen to like doing things myself, but it's not always the best solution.

### Some lessons for myself (who wrote this musing, but probably won't read it)

_**Sometimes it's best to stop with the first working solution, even if it's not particularly elegant.**_

I spent way too much time on this activity. While there are times that it's okay to spend a lot of time on an unnecessary activity, now isn't one of those times. I have a library to write.

_**Learn from your failures.**_

What did I learn? I was reminded that I get confused by Racket macros. I learned a bit about Racket macros.

_**Don't try to do everything yourself. Ask questions!**_

I should do that. I won't, but I should. Maybe some smart alum will read this and tell me the obvious solution to my cloning problem. Or maybe they'll tell me that there is no elegant solution.

---

I suppose that it's time to go back to writing the image library.

## Attempt the seventh

Fortunately, I forgot to post this musing after "finishing" it. When I woke up in the morning, I had a new idea, based on an idea I hadn't pursued.

What idea hadn't I pursued? I hadn't attempted to modify the underlying Racket code for structs. That seemed like a bad idea for many reasons. For example, how would I get the modified version to my students? Also, I'm not sure how long it would take me to grok the overall design.

However, that doesn't mean I can't define my own version of the `struct` macro that generates a call to the `struct` macro. Using the "convert syntax to data" idea from above, my `sstruct` (Sam's `struct`) can process all of its parameters and, if it finds `#:cloneable`, can replace it with the appropriate code.

Here goes [7].

```
(define-syntax sstruct
  (letrec ([sstruct-process
            (lambda (name info)
              (cond
                [(null? info)
                 null]
                [(equal? (car info) '#:cloneable)
                 (append `(#:methods gen:cloneable
                           [(define clone
                              (lambda (val) (struct-copy ,name val)))])
                         (sstruct-process name (cdr info)))]
                [else
                 (cons (car info) (sstruct-process name (cdr info)))]))])
    (lambda (stx)
      (let* ([data (syntax->datum stx)]
             [name (cadr data)]
             [info (cddr data)])
        (displayln data)
        (let ([result (cons 'struct (cons name (sstruct-process name info)))])
          (displayln result)
          (datum->syntax stx result))))))
```

The two calls to `displayln` are part of "understanding by printing". I know that there's a macro debugger, but this seems quicker.

Let's try it out.

```
> (sstruct box (contents) #:transparent #:mutable #:cloneable)
(sstruct box (contents) #:transparent #:mutable #:cloneable)
(struct box (contents) #:transparent #:mutable #:methods gen:cloneable ((define clone (lambda (val) (struct-copy box val)))))
```

So far, so good. I got what I think is the right call to the `struct` macro and I didn't get any error messages. Now, let's see if boxes are really cloneable.

```
> (define b (box "hopeful"))
> (define c (clone b))
> b
(box "hopeful")
> c
(box "hopeful")
> (set-box-contents! c "still hopeful")
> b
(box "hopeful")
> c
(box "still hopeful")
```

It worked! Yay!

---

I should see whether it works with inheritance, which was one of the issues I was dealing with.

```
> (sstruct named-box box (name) #:transparent #:mutable #:cloneable)
(sstruct named-box box (name) #:transparent #:mutable #:cloneable)
(struct named-box box (name) #:transparent #:mutable #:methods gen:cloneable ((define clone (lambda (val) (struct-copy named-box val)))))
```

I didn't expect problems defining the subclass (substructure?), but it's good to see that worked. Now, let's make sure that cloning works.

```
> (define b (named-box "hopeful" "b"))
> b
(named-box "hopeful" "b")
> (define c (clone b))
> c
(named-box "hopeful" "b")
> (set-named-box-name! c "c")
> b
(named-box "hopeful" "b")
> c
(named-box "hopeful" "c")
```

That's enough experimentation. I'm fairly confident that this approach works. 

## Attempt the eighth

You'd think I was done, wouldn't you? But I'm not. Or at least my programmer brain won't let me be. What happens when I decide to add another tag? I'll need to go back and change the code for `sstruct`. It would be much cleaner to have a procedure (or macro) like `(set-sstruct-tag tag proc)` or `(set-sstruct-tag tag arity proc)` that lets me extend `sstruct`. With that model, I would write something like the following to add `#:cloneable` to `sstruct`s.

```
(set-sstruct-tag! #:cloneable 0
                  (lambda (name)
                    `(#:methods gen:cloneable
                      [(define clone
                         (lambda (val) (struct-copy ,name val)))])))
```

I should really leave this task to another day. Nonetheless, I'm going to spend about fifteen minutes to see if I can write it. My greatest concern is how to handle scoping. I'm fairly sure that macros can only refer to a fairly limited scope of values. 

After fifteen minutes, I figured out that I can use `define-syntaxes`. Fingers crossed that I can get that right. I'll spend another fifteen minutes trying.

Can I have a shared hash table? Yes.

Unfortunately, I can't use `take` and `drop`. I wonder how the macro processor decides what procedures are available. Fortunately, I know how to write `take` and `drop`.

Here's what I've come up with after too much experimentation.

```
(define-syntaxes (sstruct set-sstruct-tag! sstruct-tags)
  (letrec ([tags (make-hash)]
           [set-tag! (lambda (tag arity code)
                       (hash-set! tag (list arity code)))]
           [take (lambda (lst n)
                   (if (zero? n)
                       null
                       (cons (car lst) (take (cdr lst) (- n 1)))))]
           [drop (lambda (lst n)
                   (if (zero? n)
                       lst
                       (drop (cdr lst) (- n 1))))]
           [sstruct-process
            (lambda (name info)
              (cond
                [(null? info)
                 null]
                [(hash-ref tags (car info) #f)
                 (let* ([arity-plus-proc (hash-ref tags (car info))]
                        [arity (car arity-plus-proc)]
                        [proc (cadr arity-plus-proc)])
                   (append (apply (eval proc) (cons name (take (cdr info) arity)))
                           (sstruct-process name (drop (cdr info) arity))))]
                [else
                 (cons (car info) (sstruct-process name (cdr info)))]))])
    (values
     ; sstruct
     (lambda (stx)
       (let* ([data (syntax->datum stx)]
              [name (cadr data)]
              [info (cddr data)])
         (displayln data)
         (let ([result (cons 'struct (cons name (sstruct-process name info)))])
           (displayln result)
           (datum->syntax stx result))))
     ; sstruct-set-tag!
     (lambda (stx)
       (let* ([data (syntax->datum stx)]
              [tag (cadr data)]
              [info (cddr data)])
         (hash-set! tags tag info))
       (datum->syntax stx '(void)))
     ; sstruct-tags
     (lambda (stx)
       (datum->syntax stx tags)))))
```

Let's see if I can define a zero-parameter tag, which I'll call `#:ignore` and should be removed.

First, we make sure that `#:ignore` is not a real tag.

```
> (sstruct box (contents) #:ignore #:transparent #:mutable)
(sstruct box (contents) #:ignore #:transparent #:mutable)
(struct box (contents) #:ignore #:transparent #:mutable)
struct: unrecognized struct-specification keyword in: #:ignore
```

So far, so good. Next, we'll add the tag.

```
> (set-sstruct-tag! #:ignore 0 (lambda (name) null))
```

Finally, we'll see if works.

```
> (sstruct box (contents) #:ignore #:transparent #:mutable)
(sstruct box (contents) #:ignore #:transparent #:mutable)
(struct box (contents) #:transparent #:mutable)
```

Looking good!

Let's try a more complicated example in which the tag says to drop the next thing in the list.

```
> (set-sstruct-tag! #:drop 1 (lambda (name val) null))
> (sstruct box (contents) #:ignore #:transparent #:drop #:foo)
(sstruct box (contents) #:ignore #:transparent #:drop #:foo)
(struct box (contents) #:transparent)
> (sstruct box (contents) #:drop #:transparent #:mutable #:drop #:foo)
(sstruct box (contents) #:drop #:transparent #:mutable #:drop #:foo)
(struct box (contents) #:mutable)
```

It worked! At some point I'm going to need to drop those "check whether it's working" lines, but they are helpful for now.

On to the most important experiment. Can I use this for cloneable objects?

```
> (sstruct box (contents) #:transparent #:mutable #:cloneable)
(sstruct box (contents) #:transparent #:mutable #:cloneable)
(struct box (contents) #:transparent #:mutable #:cloneable)
struct: unrecognized struct-specification keyword in: #:cloneable
```

```
> (set-sstruct-tag! #:cloneable 0
                    (lambda (name)
                      `(#:methods gen:cloneable
                        [(define clone
                           (lambda (val) (struct-copy ,name val)))])))
> (sstruct box (contents) #:transparent #:mutable #:cloneable)
(sstruct box (contents) #:transparent #:mutable #:cloneable)
(struct box (contents) #:transparent #:mutable #:methods gen:cloneable ((define clone (lambda (val) (struct-copy box val)))))
```

How many hours did I spend on this altogther? Five? Something like that. But I have an elegant solution.

## Additional lessons

These are for both students and for me. I'm fairly sure that they are things I say regularly.

_**Sometimes it's best to put a problem away for awhile and let your subconscious brain work on it.**_

_**Prioritize! Not everything that's useful to do is a high priority.**_ As I wrote before, I should be working on the new image library. Perhaps writing `sstruct` will make save me some time later, but it's unlikely to save five hours.

_**With enough effort, you can generate elegant solutions.**_ Perhaps that's not the best lesson for me to take from this.

---

As I said, it's time for me to go back to writing the image library.

---

Welp, that was sad. It turns out that my approach doesn't work when I try to include `sstruct.rkt` in multiple locations (e.g., in both `cloneable.rkt` and `image.rkt`). The changes in one place don't carry through. I suppose that makes sense. And it gives me another lesson.

_**Make sure to test your code in a wide variety of situations!**_

While straightforward examples worked, slightly more complex ones didn't. The solution? A bit of a kludge. I'm putting the command to add the `#:cloneable` tag in each file.

Was it worth five hours? Probably not. But I already have about twenty structs, and I'll make fewer errors writing `#:cloneable` than I will writing a nearly identical `clone` methods for each.

---

[1] Perhaps I'll remember to add the link when I post that musing.

[2] That's a lot of "I"'s in one sentence. But I'm not going to fix it.

[3] If you don't know what a shallow copy is, don't worry about it. 

[4] A macro is a procedure that acts on the text of the program.

[5] Aka "click run".

[6] Neither is elegant, so "more elegant" seems inappropriate.

[7] It wasn't quite this straightforward to write it, but I didn't take too many wrong turns.
